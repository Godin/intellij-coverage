dependencies {
  compile rootProject
}

task coverageAgentJar(type: Jar) {
  baseName = coverage_jar_name
  destinationDir = file("../dist")
  manifest.attributes(
      "Premain-Class": "com.intellij.rt.coverage.main.CoveragePremain",
      "Boot-Class-Path": "$archiveName",
      "Can-Retransform-Classes" : "true"
  )
  from(sourceSets.main.output)
  from(["util", "", "java7utils"].collect({ project(":$it").sourceSets.main.output }))
  with repackLibs
}

task soursesJar(type: Jar, dependsOn: classes) {
  baseName = coverage_jar_name
  archiveClassifier = 'sources'
  from(sourceSets.main.allSource)
  from(["util", ""].collect({ project(":$it").sourceSets.main.allSource }))
}

task allJavadoc(type: Javadoc) {
  options.tags = [ "noinspection" ]
  source = ["util", "", "instrumentation"].collect { project(":$it").sourceSets.main.allSource }
  classpath = files(["util", "", "instrumentation"].collect { project(":$it").sourceSets.main.compileClasspath })
}

task javadocJar(type: Jar) {
  baseName = coverage_jar_name
  archiveClassifier = 'javadoc'
  from allJavadoc
}

build.dependsOn(coverageAgentJar)

jar.enabled = false
artifacts {
  archives coverageAgentJar
}

jar {
  baseName = "coverage-instrumenter"
}
