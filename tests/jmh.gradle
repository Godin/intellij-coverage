/*
 * Copyright 2000-2018 JetBrains s.r.o.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply from: project(":benchmarks").file("jmh.gradle")

// Compare basic coverage with previous engine version
def PREVIOUS_VERSION = 1

// Compare new instrumentation coverage with previous engine version
def PREVIOUS_VERSION_NEW_INSTRUMENTATION = 2

// Compare basic to new instrumentation coverage
def NEW_INSTRUMENTATION = 3

def mode = PREVIOUS_VERSION

def isSampling = true
def instrumentationName = isSampling ? "sampling" : "tracing"

ext {
  baselineVersion = '1.0.684'
  benchmarkIterations = 10
  benchmarkWarmupIterations = 10
  benchmarkRegexp = 'com\\.intellij\\.rt\\.coverage\\.jmh\\.CoverageAgentBenchmark.*'
  benchmarkMode = 'ss'
  benchmarkAgentName = coverage_jar_name
  benchmarkExtraAgentArgs = "=coverage.ic false false false ${isSampling} org\\.joda.* org\\.apache\\.commons.*"
  baselineName = instrumentationName
  headName = "SNAPSHOT $instrumentationName"
}

dependencies {
  if (mode == NEW_INSTRUMENTATION) {
    baseline project(path: ':instrumentation', configuration: 'archives')
    baselineName = "SNAPSHOT $baselineName"
  } else {
    baseline "$agentsGroupId:$coverage_jar_name:$baselineVersion"
    baselineName = "$baselineVersion $baselineName"
  }
  head project(path: ':instrumentation', configuration: 'archives')
}

def setUpNewInstrumentation() {
  benchmarkExtraArgs = ['-jvmArgs', "-Didea.new.sampling.coverage=true", '-jvmArgs', "-Didea.new.tracing.coverage=true"]
}

baselineBenchmark {
  doFirst {
    if (mode == PREVIOUS_VERSION_NEW_INSTRUMENTATION) {
      setUpNewInstrumentation()
      baselineName += " (new)"
    }
  }
}

headBenchmark {
  dependsOn(":instrumentation:coverageAgentJar")
  doFirst {
    if (mode == NEW_INSTRUMENTATION || mode == PREVIOUS_VERSION_NEW_INSTRUMENTATION) {
      setUpNewInstrumentation()
      headName += " (new)"
    }
  }
}
